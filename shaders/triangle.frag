#version 450

layout(push_constant) uniform ScenePushConstants {
    vec2 cameraCenter;
    vec2 viewportSize;
    vec2 atlasGrid;
    vec2 atlasTextureSize;
    vec2 atlasTileSize;
    vec2 fogSize;
    float zoom;
    uint solidTerrainDebug;
    uint fogEnabled;
} scene;

layout(set = 0, binding = 0) uniform sampler2D uSceneAtlas;
layout(set = 0, binding = 1) uniform sampler2D uFogMask;

layout(location = 0) in vec2 vUv;
layout(location = 1) in vec2 vWorldPos;
layout(location = 2) flat in uint vSpriteIndex;
layout(location = 3) flat in uint vFlags;
layout(location = 4) in vec2 vAtlasUv;
layout(location = 5) in float vOpacity;
layout(location = 6) flat in vec4 vColor;
layout(location = 0) out vec4 outColor;

vec3 fallback_color(uint spriteIndex) {
    float idx = float(spriteIndex % 7u);
    return vec3(
        0.30 + 0.10 * mod(idx + 0.0, 3.0),
        0.45 + 0.10 * mod(idx + 1.0, 3.0),
        0.25 + 0.12 * mod(idx + 2.0, 3.0)
    );
}

void main() {
    vec4 atlasColor = texture(uSceneAtlas, vAtlasUv);
    vec3 color = atlasColor.rgb;
    float alpha = atlasColor.a * clamp(vOpacity, 0.0, 1.0);

    if ((vFlags & 32u) != 0u) {
        color = vColor.rgb;
        alpha = vColor.a * clamp(vOpacity, 0.0, 1.0);
    }

    if (scene.solidTerrainDebug != 0u && (vFlags & 8u) != 0u) {
        color = fallback_color(vSpriteIndex);
        alpha = clamp(vOpacity, 0.0, 1.0);
    }

    if ((vFlags & 2u) != 0u) {
        color = vec3(1.0, 0.2, 0.2);
        alpha = clamp(vOpacity, 0.0, 1.0);
    }

    if ((vFlags & 1u) != 0u) {
        float highlight = smoothstep(0.15, 0.85, 1.0 - distance(vUv, vec2(0.5)));
        color = mix(color, vec3(1.0, 0.9, 0.2), 0.35 * highlight);
    }

    if (scene.fogEnabled != 0u && (vFlags & 4u) == 0u) {
        vec2 fogUv = clamp(
            (vWorldPos + scene.fogSize * 0.5) / max(scene.fogSize, vec2(1.0)),
            vec2(0.0),
            vec2(1.0)
        );
        float fogValue = texture(uFogMask, fogUv).r;
        color *= mix(0.85, 1.0, fogValue);
    }

    outColor = vec4(color, alpha);
}
